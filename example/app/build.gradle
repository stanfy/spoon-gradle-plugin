apply plugin: 'com.android.application'
apply plugin: 'spoon'

android {
  compileSdkVersion 24
  buildToolsVersion '26.0.2'

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 24

    versionCode 1
    versionName '1.0'

    testApplicationId "com.stanfy.spoon.example.test"
    // We use custom test runner in this example to check how instrumentation arguments can be passed.
    // You can omit this configuration in your project build.gradle.
    testInstrumentationRunner "com.stanfy.spoon.example.test.CustomTestRunner"
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }
  flavorDimensions "main"
  productFlavors {
    free {
      dimension "main"
    }
    pro {
      dimension "main"
    }
  }

}

spoon {

  // Enable debug output
  debug = true

  // Set custom test reports directory (defaults to "spoon")
  baseOutputDir = file("$buildDir/custom-report-dir")

  // Enable setting test class/method-to-be-run from command line. E.g.:
  // $> ../gradlew spoonFreeDebugTest -PspoonClassName=com.stanfy.spoon.example.test.MainActivityTest -PspoonMethodName=testSetText
  if (project.hasProperty('spoonClassName')) {
    className = project.spoonClassName

    if (project.hasProperty('spoonMethodName')) {
      methodName = project.spoonMethodName
    }
  }

  // You can pass instrumentation arguments. Tests in this example assert this pair is passed.
  instrumentationArgs = ["foo=bar"]

  // It's also possible to configure tests sharding using convenient properties:
  // numShards = 2
  // shardIndex = 0

  adbTimeout = 30

  codeCoverage = true

  grantAllPermissions = true
}

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // for spoon snapshot
  google()
}

dependencies {
  androidTestImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.squareup.spoon:spoon-client:2.0.0-SNAPSHOT'
  androidTestImplementation 'com.android.support.test:runner:0.5'
  androidTestImplementation 'com.android.support.test:rules:0.5'
}

// for integration test of this plugin
check << {
  println "Checking spoon tasks..."
  assert project.tasks.spoon != null
  assert project.tasks.spoonFreeDebugTest != null
  assert project.tasks.spoonProDebugTest != null

  assert project.tasks.spoonFreeDebugTest.debug
  assert "$project.tasks.spoonFreeDebugTest.output".startsWith("$buildDir/custom-report-dir")

  assert project.tasks.spoonFreeDebugTest.codeCoverage

  println "SUCCESS"
}
