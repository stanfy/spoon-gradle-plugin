buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
  }
}

apply plugin: 'groovy'
apply plugin: 'nexus'
apply plugin: 'project-report'

group = 'com.stanfy.spoon'
version = '2.0.0-SNAPSHOT'

jar {
  manifest {
    attributes 'Implementation-Title' : 'Gradle plugin for Spoon',
        'Implementation-Version': version,
        'Built-By' : System.getProperty('user.name'),
        'Built-Date' : new Date(),
        'Built-JDK' : System.getProperty('java.version'),
        'Built-Gradle' : gradle.gradleVersion
  }
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenCentral()
  mavenLocal()
  google()
  jcenter()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // for spoon snapshot
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile 'com.squareup.spoon:spoon-runner:2.0.0-SNAPSHOT'

  compile('com.android.tools.build:gradle:3.0.0') {
    exclude group: 'org.jetbrains.trove4j'
  }

  testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude group: 'org.codehaus.groovy'
  }
}

modifyPom {
  project {
    name 'Spoon Gradle Plugin'
    description 'Gradle plugin for Spoon'
    url 'https://github.com/stanfy/spoon-gradle-plugin'
    inceptionYear '2013'

    scm {
      url 'https://github.com/stanfy/spoon-gradle-plugin'
      connection 'scm:git:git@github.com:stanfy/spoon-gradle-plugin.git'
      developerConnection 'scm:git:git@github.com:stanfy/spoon-gradle-plugin.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'rmazur'
        name 'Roman Mazur'
        email 'rmazur@stanfy.com.ua'
      }
    }
  }
}

task integrationTest(type: GradleBuild, dependsOn: ['check', 'install']) {
  dir = file('example')
  tasks = ['clean', 'spoon']
}
